resource "aws_sqs_queue" "graceful_termination_queue" {
  name                        = "${var.project_name}_graceful_termination_queue"
  message_retention_seconds   = 120
}

resource "aws_autoscaling_lifecycle_hook" "graceful_shutdown_asg_hook" {
  name                   = "graceful_shutdown_asg"
  autoscaling_group_name = "${aws_autoscaling_group.server_cluster.name}"
  default_result         = "CONTINUE"

  // hearbeat_timeout = 3600
  lifecycle_transition    = "autoscaling:EC2_INSTANCE_TERMINATING"
  notification_target_arn = "${aws_sqs_queue.graceful_termination_queue.arn}"
  role_arn                = "${aws_iam_role.autoscaling_role.arn}"
}

resource "aws_iam_role" "autoscaling_role" {
  name = "autoscaling_role"

  assume_role_policy = <<EOF
{
  "Version": "2012-10-17",
  "Statement": [
    {
      "Sid": "",
      "Effect": "Allow",
      "Principal": {
        "Service": "autoscaling.amazonaws.com"
      },
      "Action": "sts:AssumeRole"
    }
  ]
}
EOF
}

resource "aws_iam_role_policy" "lifecycle_hook_autoscaling_policy" {
  name = "lifecycle_hook_autoscaling_policy"
  role = "${aws_iam_role.autoscaling_role.id}"

  policy = <<EOF
{
    "Version": "2012-10-17",
    "Statement": [
        {
            "Sid": "",
            "Effect": "Allow",
            "Action": [
                "sqs:GetQueueUrl",
                "sqs:SendMessage"
            ],
            "Resource": [
                "*"
            ]
        }
    ]
}
EOF
}
